   10 MODE1
   20 VDU 23,255,255,129,255,255,126,255,255,126
   30 VDU 23,254,204,204,51,51,204,204,51,51
   31 DIM SPON$(10) 
   32 FOR K=1 TO 10:SPON$(K)=" ":NEXT 
   35 SPON$(1)="SCORPION":SPON$(3)="ANDYS CARS":SPON$(5)="JOES COLA"
   36 SPON$(7)="SNOWFIRE":SPON$(9)="MOBIL -1"
   40 RS=0
   50 HS=0:HSS$="NO-BODY"
   60 DIM LEVEL(10)
   70 DIM TRACK(15)
   80 FOR K=1 TO 10
   90 LEVEL(K)=100+((K-1)*(K*100))
  100 NEXT K
  110 CLS:PRINT TAB(10,4)"CAR RALLY":PRINT TAB(6,6)"ANDREW(SCORPION)BROOMFIELD"
  120 PRINT TAB(5,14)"PRESS S TO START OR Q TO QUIT"
  130 PRINT TAB(5,11)"HIGH SCORE ";HSS$;" : ";HS
  140 E$=GET$
  141 IF E$="Q" THEN MODE7:END 
  142 IF E$<>"S" THEN 140
  150 RS=0
  160 CHEAT=FALSE
  170 CLS
  180 LEV=1
  190 LIVES=8:XLC=0
  200 FIN=FALSE
  210 REPEAT
  220 PRINT TAB(1,1)"LEVEL ";LEV;" LEAGTH ";LEVEL(LEV);
  230 PRINT TAB(1,3)"COMPLETE IN ";((LEVEL(LEV)*12/(1+(LEV-1)/5))/100);" SECONDS"       
  240 IF LEV>5 THEN PRINT TAB(1,3)"COMPLETE IN ";((LEVEL(LEV)*12/(1+(LEV-1)/10))/100);" SECONDS                                     "
  250 PRINT TAB(1,4)"BONUS IN ";((LEVEL(LEV)*9/(1+(LEV-1)/5))/100)
  260 PRINT TAB(1,5)"SUPERBONUS 1 IN ";(((LEVEL(LEV)*11)/(1+(LEV-1)/5))/2)/100
  270 PRINT TAB(1,6)"SUPERBONUS 2 IN ";(((LEVEL(LEV)*10)/(1+(LEV-1)/5))/2)/100
  280 PRINT TAB(1,7)"SUPERBONUS 3 IN ";(((LEVEL(LEV)*9)/(1+(LEV-1)/5))/2)/100
  290 PRINT TAB(1,8)"SUPERBONUS 4 IN ";(((LEVEL(LEV)*8)/(1+(LEV-1)/5))/2)/100
  300 PRINT TAB(1,9)"SUPERBONUS 5 IN ";(((LEVEL(LEV)*7)/(1+(LEV-1)/5))/2)/100
  301 PRINT TAB(28,4)"EXTRA ";(LEV*100)
  302 PRINT TAB(28,5)"EXTRA ";(LEV*100)*2
  303 PRINT TAB(28,6)"EXTRA ";(LEV*100)*3
  304 PRINT TAB(28,7)"EXTRA ";(LEV*100)*4
  305 PRINT TAB(28,8)"EXTRA ";(LEV*100)*5
  306 PRINT TAB(28,9)"EXTRA ";(LEV*100)*6
  310 PRINT TAB(1,11)"Z=LEFT X=RIGHT K=SPEED UP M=SLOW DOWN"
  320 FOR Z=1 TO 1000000:NEXTZ
  330 PRINT TAB(1,12)"PRESS A KEY TO START" 
  340 E$=GET$
  350 CLS
  360 X=20:Y=8:L=15
  370 T=20000
  380 FOR K=1 TO 15
  390 D=RND(3)
  400 TRACK(K)=L
  410 NEXT K
  420 FOR K=1 TO 15
  430 COLOUR 2
  440 PRINT TAB(TRACK(K),23)"!              !" 
  450 COLOUR 3
  460 PRINT
  470 NEXT K
  480 S=0
  490 W=15
  500 RECT=0
  510 TIME=0
  520 REPEAT
  530 CRASH=FALSE
  540 IF X<TRACK(1)+1 THEN CRASH=TRUE 
  550 IF X>TRACK(1)+W-1 THEN CRASH=TRUE 
  560 FOR K=1 TO 14
  570 TRACK(K)=TRACK(K+1)
  580 NEXT K
  590 D=RND(3)
  600 IF D=1 THEN L=L-1
  610 IF D=3 THEN L=L+1
  620 IF L<3 THEN L=L+1
  630 IF L>21 THEN L=L-1    
  640 TRACK(15)=L
  650 IF S>1500 THEN W=14 
  660 IF S>2500 THEN W=13
  670 IF S>3500 THEN W=12
  680 IF S>5000 THEN W=11
  690 IF S>7000 THEN W=10
  700 IF S>8000 THEN W=9
  710 IF S>8900 THEN W=8
  720 R=L+W
  730 IF S+115>LEVEL(LEV) THEN COLOUR 1 ELSE COLOUR 3
  731 SPIN=RND(50):IF SPIN>10 THEN GOTO 740
  732 IF L>15 THEN PRINT TAB(L-12,23);SPON$(SPIN) 
  733 IF R<25 THEN PRINT TAB(R+2,23);SPON$(SPIN)
  740 PRINT TAB(L,23);"!";TAB(R,23);"!"
  750 COLOUR 3
  760 IF S+15>LEVEL(LEV) THEN PRINT TAB(L,23);CHR$(254);TAB(R,23);CHR$(254)
  770 PRINT
  780 S=S+1:XLC=XLC+1
  790 FOR K=1 TO T:NEXT K
  800 PX=X
  810 A$=INKEY$(0)
  820 IF A$="P" THEN E$=GET$:TIME=RECT
  830 IF A$="Z" THEN X=X-1
  840 IF A$="X" THEN X=X+1
  850 IF A$="Q" AND CHEAT=TRUE THEN CHEAT=FALSE  
  860 IF A$="C" AND CHEAT=FALSE THEN CHEAT=TRUE
  870 IF A$="K" THEN T=T-500
  880 IF T<0 THEN T=0
  890 IF A$="M" THEN T=T+500
  900 IF T>20000 THEN T=20000
  910 PRINT TAB(1,1)"TIME ";RECT/100;
  920 PRINT TAB(13,1)"DISTANCE LEFT ";LEVEL(LEV)-S;" "
  930 PRINT TAB(32,1)"CARS ";LIVES;" "
  940 PRINT TAB(PX,Y-1)" "
  950 PRINT TAB(X,Y)CHR$(255)
  960 RECT=TIME 
  970 IF CHEAT=TRUE THEN CRASH=FALSE
  980 IF CRASH=TRUE THEN GOSUB 1510
  990 IF S>LEVEL(LEV) THEN CRASH=TRUE
 1000 UNTIL CRASH=TRUE
 1010 IF S<LEVEL(LEV) THEN 1250
 1020 CLS
 1030 PRINT TAB(1,1)"YOU FINISHED LEVEL ";LEV
 1040 PRINT TAB(1,2)"AND YOUR TIME WAS ";RECT/100
 1050 IF LEV<6 AND RECT<(LEVEL(LEV)*9)/(1+(LEV-1)/5) THEN 1300 
 1060 IF LEV>5 AND RECT<(LEVEL(LEV)*9)/(1+(LEV-1)/10) THEN 1300
 1070 IF LEV<6 AND RECT>(LEVEL(LEV)*12)/(1+(LEV-1)/5) THEN 1400 
 1080 IF LEV>5 AND RECT>(LEVEL(LEV)*12)/(1+(LEV-1)/10) THEN 1400
 1090 FIN=FALSE
 1100 RS=RS+S
 1110 IF LEV>9 THEN FIN=TRUE       
 1120 LEV=LEV+1
 1130 PRINT TAB(1,5)"YOUR SCORE IS ";RS
 1131 IF XLC>10000 THEN LIVES=LIVES+1:XLC=XLC-10000:PRINT TAB(1,6)"YOU GET AN EXTRA CAR"
 1140 FOR Z=1 TO 1000000:NEXTZ
 1150 PRINT TAB(1,6)"PRESS A KEY                               "
 1160 CLS
 1170 UNTIL FIN=TRUE
 1180 CLS
 1190 IF LEV>10 THEN FOR K=1 TO 255:SOUND 1,-15,K,2:NEXT K:PRINT TAB(10,1)"FINISHED!!"
 1191 PRINT TAB(2,3)"GAME SCORE : ";RS
 1192 PRINT TAB(2,4)"LAST LEVEL BONUS (";LEV;" x ";1000;") : ";LEV*1000  
 1193 PRINT TAB(2,5)"CARS LEFT BONUD (";LIVES;" x ";1000;") : ";LIVES*1000
 1194 RS=RS+(LEV*1000)+(LIVES*1000)
 1195 PRINT TAB(2,7)"FINAL SCORE : ";RS
 1200 IF RS<=HS THEN 1450
 1210 PRINT TAB(2,9)"THATS A HIGH SCORE!!!" 
 1220 PRINT TAB(2,10)"ENTER NAME >";:INPUT HSS$ 
 1230 HS=RS
 1240 GOTO 1450
 1250 PRINT TAB(1,1)"YOU CRASHED YOU DOPE                                 "
 1260 PRINT TAB(1,2)"BUT DID COMPLETE ";S;" PARTS"
 1270 RS=RS+S
 1290 FIN=TRUE:GOTO 1140
 1300 PRINT TAB(1,3)"THAT MEANS YOU GET A BONUS"
 1310 BONUS=LEV*100
 1320 IF RECT<((LEVEL(LEV)*11)/(1+(LEV-1)/5))/2 THEN BONUS=BONUS*2:PRINT TAB(22,3)"SUPERBONUS"  
 1330 IF RECT<((LEVEL(LEV)*10)/(1+(LEV-1)/5))/2 THEN BONUS=BONUS/2*3
 1340 IF RECT<((LEVEL(LEV)*9)/(1+(LEV-1)/5))/2 THEN BONUS=BONUS/3*4
 1350 IF RECT<((LEVEL(LEV)*8)/(1+(LEV-1)/5))/2 THEN BONUS=BONUS/4*5
 1360 IF RECT<((LEVEL(LEV)*7)/(1+(LEV-1)/5))/2 THEN BONUS=BONUS/5*6
 1370 PRINT TAB(1,4)"YOU SCORE AN EXTRA ";BONUS
 1380 RS=RS+BONUS:XLC=XLC+BONUS
 1390 GOTO 1100
 1400 PRINT TAB(1,3)"NOT FAST ENOUGH!!"
 1410 PRINT TAB(1,4)"YOU CAN'T GO ON"
 1420 RS=RS+S
 1430 PRINT TAB(1,5)"YOU SCORE ";RS
 1440 FIN=TRUE:GOTO 1140
 1450 FOR ZXY=1 TO 100000:NEXT ZXY
 1455 PRINT TAB(2,13)"PRESS A KEY":WAIT$=GET$ 
 1460 GOTO 110
 1510 LIVES=LIVES-1
 1520 T=20000
 1530 IF X<TRACK(1) THEN X=TRACK(1)+4 ELSE X=TRACK(1)+W-4
 1540 CRASH=FALSE
 1550 IF LIVES<1 THEN CRASH=TRUE
 1560 RETURN
