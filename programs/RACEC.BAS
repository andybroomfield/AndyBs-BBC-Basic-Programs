   10 MODE 1
   20 VDU 19,1,1,0,0,0
   30 GOSUB 1150
   40 MAXCOU=12
   50 DIM HIS(6):DIM HISB$(6)
   60 FOR K=1 TO 6
   70 HIS(K)=0:HISB$(K)="NO BODY"
   80 NEXT
   90 DIM TRACK(45):DIM DRT(45):DIM LEVEL(10) 
  100 FOR K=1 TO 10
  110 LEVEL(K)=150+((K-1)*(K*150))
  120 NEXT
  130 GOTO 1580
  140 L=350:SP=3.2:W=600:ID=4:RS=0:LEV=0:LIVES=8:MR=10:HILL=0:LLIH=30:LOP=10
  150 X=500:Y=5*SP+(200-SP*4)+23:PLAY=(3.2+(SQR((3.2-SP)^2)))*7*100 DIV 100   
  151 FOR K=1 TO 31
  152 TRACK(K)=L:DRT(K)=L  
  153 NEXT
  160 MR=(LEV+1) DIV 2 *20:W=615-(LEV*15):VR=(LEV)*20
  170 DIS=0
  210 T=20000:RTD=20000     
  220 S=0:VDU4
  230 LEV=LEV+1:FIN=FALSE
  240 GOSUB 1330
  250 TIME=0:VDU5:X=TRACK(5)+200:DIS=0:COU=0     
  260 GCOL 0,3:GOSUB 1000
  270 REPEAT
  280 VDU5
  290 FOR K=1 TO 30
  300 TRACK(K)=TRACK(K+1)
  310 NEXT K
  320 D=RND(3)
  330 IF D=1 THEN L=L-RND(MR)
  340 IF D=3 THEN L=L+RND(MR)
  350 IF L<100 THEN L=100   
  360 IF L>1200-W THEN L=1200-W
  370 TRACK(31)=L   
  380 S=S+1
  390 FOR K=1 TO RTD:NEXT K  
  400 PX=X
  410 A$=INKEY$(0)
  420 IF A$<>"P" THEN 480
  430 RECT=TIME
  440 REPEAT
  450 WI$=INKEY$(0)
  460 UNTIL WI$<>""
  470 TIME=RECT
  480 IF A$="Z" THEN DIS=DIS-5:COU=0
  490 IF DIS<-15 THEN DIS=-15
  500 IF A$="X" THEN DIS=DIS+5:COU=0  
  510 IF DIS>15 THEN DIS=15
  520 MAXCOU=17-(SQR(DIS^2))
  530 COU=COU+1
  540 IF COU>MAXCOU AND DIS>0 THEN DIS=DIS-5:COU=0
  550 IF COU>MAXCOU AND DIS<0 THEN DIS=DIS+5:COU=0
  560 X=X+DIS
  570 IF A$="K" THEN T=T-500
  580 IF T<0 THEN T=0
  590 IF A$="M" THEN T=T+500
  600 IF T>20000 THEN T=20000
  605 RTD=(SP-1.5)*200+T       
  610 GCOL 0,0:GOSUB 1000
  620 GCOL 0,0:MOVE PX,Y:PRINT CHR$(250)
  630 GCOL 0,0:MOVE 0,PLAY*SP+(200-SP*4):DRAW 1260,PLAY*SP+(200-SP*4)  
  640 GO=RND(300)
  650 IF LLIH<=HILL THEN 700
  651 IF SP<3.18 THEN SP=SP+0.01:GOTO 750 
  652 IF SP>3.2 THEN SP=SP-0.01:GOTO 750  
  660 IF GO>VR THEN 750
  670 HILL=RND(100)+LEV*20 
  680 LLIH=0
  690 V=RND(3)
  700 LLIH=LLIH+1
  710 IF V=1 THEN SP=SP-0.01
  720 IF V=2 THEN SP=SP+0.01
  730 IF SP<1.5 THEN SP=1.5
  740 IF SP>4.5 THEN SP=4.5
  750 FOR K=1 TO 31:DRT(K)=TRACK(K):NEXT  
  760 PLAY=(3.2+(SQR((3.2-SP)^2)))*7*100 DIV 100   
  770 LOP=LOP-1
  780 IF LOP<1 THEN LOP=10
  790 GCOL 0,3:GOSUB 1000
  800 GCOL 0,3
  810 MOVE 0,PLAY*SP+(200-SP*4):DRAW 1260,PLAY*SP+(200-SP*4)  
  820 GCOL 0,1:MOVE X,Y:PRINT CHR$(250):GCOL 0,3 
  830 GOSUB 1110
  840 VDU4:PRINT TAB(1,1)"DISTANCE ";LEVEL(LEV)-S;" "    
  850 PRINT TAB(16,1)" TIME ";LIMIT-(TIME DIV 100);" "  
  860 PRINT TAB(31,1)" CARS ";LIVES               
  870 IF S=LEVEL(LEV) THEN CRASH=TRUE:FIN=TRUE
  880 VDU5
  890 UNTIL CRASH=TRUE
  900 VDU4
  910 DIS=0:COU=MAXCOU
  920 IF FIN=TRUE THEN GOTO 1170 
  930 VDU5
  940 LIVES=LIVES-1:T=70000
  950 GCOL 0,0:MOVE X,Y:PRINT CHR$(250):GCOL 0,3  
  951 A$=" "
  960 IF LIVES>0 THEN X=TRACK(5)+200:GOTO 270   
  970 VDU4
  980 FOR K=1 TO 900000:NEXT
  990 GOTO 1420
 1000 MOVE DRT(1),200-SP*4
 1010 FOR DT=2 TO 31
 1011 IF DT>PLAY THEN 1040
 1020 DRAW DRT(DT)+DT*ID,DT*SP+(200-SP*4)
 1030 IF (DT-LOP) MOD 10=0 THEN DRAW DRT(DT)+DT*ID,DT*SP+(200-SP*4)+150-(DT*SP):MOVE DRT(DT)+DT*ID,DT*SP+(200-SP*4) 
 1040 NEXT
 1050 MOVE DRT(1)+W,200-SP*4    
 1060 FOR DT=2 TO 31
 1061 IF DT>PLAY THEN 1090
 1070 DRAW DRT(DT)+W-DT*ID,DT*SP+(200-SP*4)    
 1080 IF (DT-LOP) MOD 10=0 THEN DRAW DRT(DT)+W-DT*ID,DT*SP+(200-SP*4)+150-(DT*SP):MOVE DRT(DT)+W-DT*ID,DT*SP+(200-SP*4)    
 1090 NEXT
 1100 RETURN
 1110 CRASH=FALSE 
 1120 IF X<TRACK(5)+5*ID THEN CRASH=TRUE  
 1130 IF X+25>TRACK(5)+W-5*ID THEN CRASH=TRUE  
 1140 RETURN
 1150 VDU 23,250,0,0,0,255,66,126,255,129
 1160 RETURN
 1170 CLS:PRINT "FINISH STAGE ";LEV   
 1180 RECT=TIME
 1190 PRINT "SCORE IS CURRENTLY ";RS
 1200 PRINT "DISTACE ACHIVED ";S
 1210 PRINT "TIME LEFT ";LIMIT-RECT/100
 1220 IF RECT/100>LIMIT THEN 1440 
 1230 BONUS=(LIMIT-(RECT/100))*100  
 1240 PRINT "BONUS ";BONUS
 1250 RS=RS+BONUS
 1260 RS=RS+S:PRINT "SCORE IS NOW ";RS   
 1270 FOR K=1 TO 100000:NEXT
 1271 PRINT "PRESS A KEY"
 1280 REPEAT:WI$=INKEY$(0):UNTIL WI$=""    
 1290 WI$=GET$
 1300 CLS:S=0
 1310 IF LEV=10 THEN PRINT "YOU'VE FINISHED!!":GOTO 1460
 1320 GOTO 160
 1330 CLS:PRINT "STAGE ";LEV  
 1340 PRINT "DISTANCE ";LEVEL(LEV)
 1350 LIMIT=((LEVEL(LEV)*(16-LEV)/(1+(LEV-1)/10))DIV 100)  
 1360 PRINT "COMPLETE IN ";LIMIT;" SECONDS"
 1361 PRINT:PRINT:PRINT "Z=LEFT X=RIGHT K=SPEED UP M=SLOW DOWN"
 1370 FOR K=1 TO 100000:NEXT   
 1379 PRINT "PRESS A KEY"
 1380 REPEAT:WI$=INKEY$(0):UNTIL WI$=""
 1390 WI$=GET$
 1400 CLS
 1410 RETURN
 1420 CLS:PRINT "YOU CRASHED TOO MANY TIMES"    
 1430 GOTO 1450
 1440 PRINT "THATS NOT FAST ENOUGH!!!!!"
 1450 RS=RS+S
 1460 PRINT "YOUR SCORE IS ";RS
 1461 PRINT:PRINT "LAST STAGE BONUS ";LEV;" x 1000 = ";LEV*10000   
 1462 RS=RS+(LEV*10000)  
 1469 PRINT:PRINT "FINAL SCORE : ";RS
 1470 REPEAT:WI$=INKEY$(0):UNTIL WI$="" 
 1480 P=6
 1490 FOR K=5 TO 1 STEP-1
 1500 IF RS>HIS(K) THEN P=K:HIS(K)=HIS(K-1):HISB$(K)=HISB$(K-1)
 1510 NEXT
 1511 PRINT
 1520 IF P<6 THEN INPUT "HI SCORE ENTER NAME ";HISB$(P):HIS(P)=RS 
 1530 FOR K=1 TO 5
 1540 PRINT TAB(3,10+K);K;". ";HISB$(K);
 1541 PRINT TAB(18);HIS(K)
 1550 NEXT
 1560 PRINT:PRINT:PRINT "PRESS A KEY"
 1570 WI$=GET$:CLS
 1580 PRINT TAB(10,4)"CAR RALLY TWO"
 1590 PRINT TAB(7,6)"BY ANDREW(SCORPION)BROOMFIELD"
 1600 PRINT TAB(7,10)"PRESS S TO START OR Q TO QUIT"
 1610 WI$=GET$
 1620 IF WI$="S" THEN 140
 1630 IF WI$<>"Q" THEN 1610
 1640 MODE7:END
