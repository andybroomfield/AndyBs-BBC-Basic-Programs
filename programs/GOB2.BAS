   10 MODE 1:GOTO 1140
   20 DIM CAN(20,20)
   30 PPP=1
   40 DIM XG(2):DIM YG(2)
   50 DIM PXG(2):DIM PYG(2)
   60 HS=0:HSB$="NO BODY"
   70 GOTO 1040
   80 LEVEL=0:SPA=340:TL=31:S=0:LIVES=5:BP=0:ELS=0    
   90 LEVEL=LEVEL+1:SPA=SPA-20:TL=TL-1:BP=BP+1:GM=FALSE    
  100 COLOUR 3:PRINT TAB(22,1)"LEVEL : ";LEVEL:PRINT TAB(22,2)"TIME : ";TL
  110 IF SPA<66 THEN CLS:PRINT "FINSISH!!":GOTO 960   
  120 FOR X=1 TO 20
  130 FOR Y=1 TO 20
  140 CAN(X,Y)=2
  150 PRINT TAB(X,Y)"H"
  160 NEXT Y 
  170 NEXT X
  180 X=10:Y=10:COU=0 
  190 XT=2:YT=2
  200 REPEAT
  210 PRINT TAB(X,Y)"."
  220 IF CAN(X,Y)=1 THEN COU=COU-1
  230 CAN(X,Y)=1  
  240 COU=COU+1
  250 IF X>XT THEN X=X-1:GOTO 290 
  260 IF X<XT THEN X=X+1:GOTO 290 
  270 IF Y<YT THEN Y=Y+1 
  280 IF Y>YT THEN Y=Y-1
  290 IF XT=X AND YT=Y THEN XT=RND(18)+1:YT=RND(18)+1
  300 IF COU=19 THEN XT=19:YT=19  
  310 IF COU=65 THEN XT=10:YT=10   
  320 UNTIL COU=SPA
  330 XP=10:YP=10:N=0:PX=XP:PY=YP    
  340 XG(1)=19:YG(1)=19
  350 XG(2)=2:YG(2)=2
  360 PRINT TAB(XP,YP);CHR$(241)
  370 FOR K=1 TO 2:PRINT TAB(XG(K),YG(K))"A":NEXT K
  380 A$=GET$:TIME=0:GOTO 420
  390 PX=XP:PY=YP
  400 FOR TD=1 TO 16500:NEXT 
  410 A$=INKEY$(0)
  420 IF A$="A" THEN N=1
  430 IF A$="Z" THEN N=2
  440 IF A$="O" THEN N=3
  450 IF A$="P" THEN N=4
  460 IF A$<>"S" THEN 500
  470 RECT=TIME
  480 WI$=GET$
  490 TIME=RECT
  500 IF N=1 THEN YP=YP-1
  510 IF N=2 THEN YP=YP+1
  520 IF N=3 THEN XP=XP-1
  530 IF N=4 THEN XP=XP+1
  540 IF CAN(XP,YP)=2 THEN XP=PX:YP=PY 
  550 PRINT TAB(22,3)"SCORE : ";S   
  560 PRINT TAB(22,2)"TIME : ";TL-TIME DIV 100;" "
  570 PRINT TAB(22,4)"LIVES : ";LIVES
  580 COLOUR 0:PRINT TAB(PX,PY)"X"
  590 COLOUR 1:PRINT TAB(XP,YP);CHR$(240+N)
  600 DIE=FALSE
  610 IF TIME>TL*100 THEN 90
  620 IF GM=TRUE THEN GM=FALSE ELSE GM=TRUE
  630 IF CAN(XP,YP)=1 THEN 830   
  640 FOR K=1 TO 2
  650 PPP=1
  660 IF GM=FALSE THEN 740
  670 IF XG(K)>XP THEN XG(K)=XG(K)-1:PPP=0
  680 IF XG(K)<XP THEN XG(K)=XG(K)+1:PPP=0
  690 IF CAN(XG(K),YG(K))=2 THEN XG(K)=PXG(K):PPP=1
  700 IF PPP=0 THEN 730
  710 IF YG(K)>YP THEN YG(K)=YG(K)-1 
  720 IF YG(K)<YP THEN YG(K)=YG(K)+1    
  730 IF CAN(XG(K),YG(K))=2 THEN XG(K)=PXG(K):YG(K)=PYG(K)
  740 IF CAN(PXG(K),PYG(K))=1 THEN COLOUR 3:PRINT TAB(PXG(K),PYG(K))"."
  750 IF CAN(PXG(K),PYG(K))=0 THEN COLOUR 0:PRINT TAB(PXG(K),PYG(K))"X" 
  760 COLOUR 2:PRINT TAB(XG(K),YG(K))"A"
  770 IF XG(K)=XP AND YG(K)=YP THEN DIE=TRUE
  780 PXG(K)=XG(K):PYG(K)=YG(K)
  790 NEXT K   
  800 IF DIE=TRUE THEN 880
  810 IF XG(1)=XG(2) AND YG(1)=YG(2) THEN XG(1)=19:YG(1)=19:XG(2)=2:YG(2)=2
  820 GOTO 390 
  830 S=S+BP
  840 ELS=ELS+BP
  850 IF ELS>1000 THEN LIVES=LIVES+1:ELS=ELS-1000
  860 CAN(XP,YP)=0  
  870 GOTO 640
  880 LIVES=LIVES-1
  890 XP=10:YP=10
  900 XG(1)=19:YG(1)=19:XG(2)=2:YG(2)=2
  910 FOR TD=1 TO 30000:NEXT 
  920 SOUND 0,-15,20,10
  930 IF LIVES>0 THEN 390
  940 CLS:COLOUR3
  950 PRINT "YOUR CAUGHT!!!!"
  960 PRINT "YOUR SCORE IS:";S
 1000 IF S>HS THEN INPUT "NAME ";HSB$:HS=S  
 1010 A$=INKEY$(0)
 1020 IF A$<>"" THEN 1010
 1030 FOR K=1 TO 1000000:NEXT:PRINT "PRESS A KEY":A$=GET$
 1040 CLS:PRINT TAB(10,5)"GOBBLE 2"
 1050 PRINT TAB(6,7)"BY ANDREW(SCORPION)BROOMFIELD"
 1060 PRINT TAB(10,9)"HIGH SCORE"
 1070 PRINT TAB(10,10);HSB$;" : ";HS
 1080 PRINT TAB(4,14)"PRESS A KEY TO PLAY OR E TO EXIT" 
 1090 A$=GET$
 1100 IF A$="E" THEN MODE7:END
 1110 IF A$="" THEN 1090 
 1120 CLS
 1130 GOTO 80
 1140 VDU 23,241,129,66,36,102,126,60,190,129
 1150 VDU 23,242,129,90,60,126,102,36,66,129  
 1160 VDU 23,243,129,90,60,14,14,60,90,129  
 1170 VDU 23,244,129,90,60,112,112,60,90,129 
 1180 GOTO 20
