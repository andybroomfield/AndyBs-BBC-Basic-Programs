   10 MODE 1
   20 COL1=RND(15):COL2=RND(15):COL3=RND(15)
   30 VDU 19,1,2,0,0,0:VDU 19,2,3,0,0,0
   40 VDU 19,3,1,0,0,0
   50 VDU 23,253,255,255,255,255,255,255,255,255
   60 VDU 23,254,122,112,250,243,243,250,112,122
   65 VDU 23,244,126,30,127,207,207,127,30,126
   70 VDU 23,255,248,60,94,255,255,94,60,248  
   75 VDU 23,245,31,60,122,255,255,122,60,31
   80 UP$="A":DOWN$="Z":LEF$="O":RIG$="P":FIR$="Q"
   90 VDU 5
  100 CLS:CLG
  110 MOVE 0,700
  120 GCOL 0,1
  130 PRINT "STAR BATTLE":PRINT "BY ANDREW($CORPION)BROOMFIELD"   
  140 PRINT "DESTROY AS MANY SHIPS AS YOU CAN"
  150 PRINT UP$;"=UP ";DOWN$;"=DOWN ";LEF$;"=LEFT ";RIG$;"=RIGHT"
  160 PRINT FIR$;"=FIRE"
  170 PRINT "PRESS P TO PLAY OR K TO CHANGE KEYS"
  180 F$=GET$
  190 IF F$="K" THEN GOSUB 910
  200 IF F$<>"P" THEN GOTO 100
  210 CLS:CLG:GCOL 0,3
  220 X=100:Y=550:XE=RND(700)+300:YET=RND(2):YE=1000
  230 IF YET=2 THEN YE=0
  235 DIR=2:DIRE=1
  240 CBFP=0:CBFE=0
  250 M=0:N=0:ME=0:NE=0
  260 DES=0
  270 PX=X:PY=Y:PXE=XE:PYE=YE
  280 FIREP=FALSE:FIREE=FALSE
  290 DIE=FALSE
  300 A$=INKEY$(0)
  310 IF A$=UP$ THEN M=M+2
  320 IF A$=DOWN$ THEN M=M-2
  330 IF A$=LEF$ THEN N=N-2:DIR=1
  340 IF A$=RIG$ THEN N=N+2:DIR=2
  350 IF M>30 THEN M=30
  360 IF M<-30 THEN M=-30
  370 IF N>30 THEN N=30
  380 IF N<-30 THEN N=-30
  390 X=X+N:Y=Y+M
  400 IF X>1200 THEN X=1180
  410 IF X<0 THEN X=20
  420 IF Y>800 THEN Y=780
  430 IF Y<30 THEN Y=50
  440 IF A$=FIR$ THEN FIREP=TRUE  
  450 CBFP=CBFP+1
  460 IF CBFP<6 THEN FIREP=FALSE
  470 IF X<XE THEN XE=XE-10:DIRE=1
  480 IF X>XE THEN XE=XE+10:DIRE=2
  490 IF Y<YE THEN YE=YE-10
  500 IF Y>YE THEN YE=YE+10
  510 IF Y+30>YE AND Y-30<YE THEN FIREE=TRUE 
  520 CBFE=CBFE+1
  530 IF CBFE<6 THEN FIREE=FALSE     
  540 GCOL 0,0:MOVE PX,PY:PRINT CHR$(253)
  550 MOVE PXE,PYE:PRINT CHR$(253)
  560 MOVE PX-200,PY:PRINT CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253)    
  570 MOVE PXE-150,PYE:PRINT CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253);CHR$(253) 
  580 GCOL 0,3:MOVE X,Y
  581 IF DIR=1 THEN PRINT CHR$(244)
  582 IF DIR=2 THEN PRINT CHR$(254)
  590 GCOL 0,1:MOVE XE,YE
  591 IF DIRE=1 THEN PRINT CHR$(255)
  592 IF DIRE=2 THEN PRINT CHR$(245)
  600 GCOL 0,2
  610 IF FIREP=TRUE AND DIR=1 THEN MOVE X,Y-14:DRAW X-200,Y-14
  611 IF FIREP=TRUE AND DIR=2 THEN MOVE X,Y-14:DRAW X+200,Y-14 
  620 IF FIREE=TRUE AND DIRE=1 THEN MOVE XE,YE-14:DRAW XE-150,YE-14
  622 IF FIREE=TRUE AND DIRE=2 THEN MOVE XE,YE-14:DRAW XE+150,YE-14
  630 FOR K=1 TO 10000:NEXT K  
  640 IF FIREP=TRUE THEN GOSUB 760
  650 IF FIREE=TRUE THEN GOSUB 860
  660 IF X+30>XE AND X-30<XE AND Y+30>YE AND Y-30<YE THEN DIE=TRUE   
  670 IF DIE=FALSE THEN GOTO 270
  680 GCOL 0,1:MOVE X,Y:PRINT "*":SOUND 0,-15,1,10
  690 VDU 4
  700 PRINT TAB(1,1)"GAME OVER"   
  710 PRINT TAB(1,2)"YOU DESTROYED ";DES;" SHIPS"    
  720 PRINT TAB(1,4)"PLAY AGAIN (Y/N) ";:INPUT OPT$
  730 IF OPT$="YES" OR OPT$="Y" THEN GOTO 90
  740 IF OPT$="NO" OR OPT$="N" THEN END
  750 GOTO 720
  760 KILL=FALSE
  770 IF DIR=1 AND XE>X-200 AND XE<X AND YE>Y-14 AND YE-28<Y-14 THEN KILL=TRUE
  780 IF DIR=2 AND XE<X+200 AND XE>X AND YE>Y-14 AND YE-28<Y-14 THEN KILL=TRUE
  790 IF KILL=TRUE THEN DES=DES+1
  800 IF KILL=TRUE THEN GCOL 0,0:MOVE XE,YE:PRINT CHR$(253) 
  810 IF KILL=TRUE THEN XE=RND(700)+300:YE=1000:YET=RND(2)
  820 IF KILL=TRUE AND YET=2 THEN YE=0
  830 CBFP=0
  840 SOUND 1,-15,100,1
  850 RETURN
  860 IF DIRE=1 AND X+28>XE-150 AND X<XE AND Y>YE-14 AND Y-28<YE-14 THEN DIE=TRUE
  870 IF DIRE=2 AND X+28<XE+150 AND X>XE AND Y>YE-14 AND Y-28<YE-14 THEN DIE=TRUE     
  880 CBFE=0
  890 SOUND 3,-15,55,1
  900 RETURN
  910 CLG
  920 MOVE 100,800
  930 PRINT "UP=";:UP$=GET$:PRINTUP$
  940 PRINT "DOWN=";:DOWN$=GET$:PRINT DOWN$
  950 PRINT "LEFT=";:LEF$=GET$:PRINT LEF$
  960 PRINT "RIGHT=";:RIG$=GET$:PRINT RIG$
  970 PRINT "FIRE=";:FIR$=GET$:PRINT FIR$
  980 F$=GET$
  990 CLG
 1000 RETURN
