   10 MODE1
   20 PR=10:LD=0
   30 GOSUB 950
   40 GOTO 1080
   50 GRAV=0.1:LD=0:DP1=8:DP2=8:BP1=5:BP2=5:WP1=3:WP2=3
   60 X1=RND(400)+100:X2=RND(400)+550
   70 Y1=1000:Y2=1000
   80 H=RND(200)+150:IH=0   
   90 CLS:CLG
  100 GCOL 0,3
  110 IHS=RND(100)
  120 FOR K=1 TO 1000 STEP 3 
  130 IF K>=X1-1 AND K<=X1+33 THEN 180
  140 IF K>=X2-1 AND K<=X2+33 THEN 180
  150 IH=RND(IHS)-IHS/2:H=H+IH
  160 IF H>600 THEN H=600
  170 IF H<50 THEN H=200   
  180 MOVE K,0:DRAW K,H
  190 NEXT
  200 GOSUB 1230
  210 IF TURN=2 THEN 430      
  220 VDU4
  230 COLOUR1:PRINT TAB(32,3)"PLAYER 1":COLOUR3
  240 PRINT TAB(32,5)"ANGLE":PRINT TAB(32,8)"SPEED"   
  250 PRINT TAB(32,6);"       ":PRINT TAB(32,6);:INPUT A:PRINT TAB(32,9)"        ":PRINT TAB(32,9);:INPUT U  
  260 IF U>10 THEN GOTO 250 
  270 RA=RAD(A)
  280 UH=U*(COS(RA)):UV=U*(SIN(RA))    
  290 DIG=FALSE:BOMB=FALSE:WALL=FALSE:PRINT TAB(32,11)"A:NORMAL":PRINT TAB(32,12)"B:DIG x";DP1     
  300 PRINT TAB(32,13)"C:BOMBx";BP1:PRINT TAB(32,14)"D:WALLx";WP1    
  310 PRINT TAB(32,20)"Z:CANCEL"
  320 SELECT$=GET$
  330 IF SELECT$="A" THEN 390   
  340 IF SELECT$="B" AND DP1>0 THEN DIG=TRUE:DP1=DP1-1:GOTO 390 
  350 IF SELECT$="C" AND BP1>0 THEN BOMB=TRUE:BP1=BP1-1:GOTO 390
  360 IF SELECT$="D" AND WP1>0 THEN WALL=TRUE:WP1=WP1-1:GOTO 390
  370 IF SELECT$="Z" THEN GOTO 230
  380 GOTO 290
  390 COLOUR3
  400 PRINT TAB(32,20)"FIRE!!! "
  410 FIRE=1:GOSUB 660
  420 IF KILL1=TRUE OR KILL2=TRUE THEN 990
  430 GOSUB 1230
  440 COLOUR2:PRINT TAB(32,3)"PLAYER 2":COLOUR3
  450 PRINT TAB(32,5)"ANGLE":PRINT TAB(32,8)"SPEED"
  460 PRINT TAB(32,6);"       ":PRINT TAB(32,6);:INPUT A:PRINT TAB(32,9)"        ":PRINT TAB(32,9);:INPUT U  
  470 IF U>10 THEN GOTO 460
  480 RA=RAD(A) 
  490 UH=-U*(COS(RA)):UV=U*(SIN(RA)) 
  500 DIG=FALSE:BOMB=FALSE:WALL=FALSE:PRINT TAB(32,11)"A:NORMAL":PRINT TAB(32,12)"B:DIG x";DP2  
  510 PRINT TAB(32,13)"C:BOMBx";BP2:PRINT TAB(32,14)"D:WALLx";WP2   
  520 PRINT TAB(32,20)"Z:CANCEL"
  530 SELECT$=GET$  
  540 IF SELECT$="A" THEN 600    
  550 IF SELECT$="B" AND DP2>0 THEN DIG=TRUE:DP2=DP2-1:GOTO 600
  560 IF SELECT$="C" AND BP2>0 THEN BOMB=TRUE:BP2=BP2-1:GOTO 600
  570 IF SELECT$="D" AND WP2>0 THEN WALL=TRUE:WP2=WP2-1:GOTO 600
  580 IF SELECT$="Z" THEN GOTO 440
  590 GOTO 500
  600 COLOUR3
  610 PRINT TAB(32,20)"FIRE!!! "
  620 FIRE=2:GOSUB 660  
  630 IF KILL1=TRUE OR KILL2=TRUE THEN 990
  640 GOSUB 1230   
  650 GOTO 220
  660 ENFI=FALSE
  670 IF FIRE=1 THEN XT=X1+15:YT=Y1+2
  680 IF FIRE=2 THEN XT=X2+15:YT=Y2+2
  690 KILL1=FALSE:KILL2=FALSE
  700 IF DIG=TRUE THEN LD=5
  710 REPEAT
  720 FOR TD=1 TO 5000:NEXT
  730 PXT=XT:PYT=YT
  740 XT=XT+UH:YT=YT+UV
  750 GCOL 0,0:PLOT 69,PXT,PYT  
  760 UV=UV-GRAV
  770 IF BOMB=TRUE AND UV<0 THEN UH=0
  780 HIT=FALSE:GOTO 790
  790 IF POINT(XT,YT)>0 THEN HIT=TRUE
  800 IF POINT(XT,YT)=1 THEN KILL1=TRUE
  810 IF POINT(XT,YT)=2 THEN KILL2=TRUE
  820 IF HIT=TRUE THEN ENFI=TRUE
  830 IF XT<0 OR XT>1000 THEN ENFI=TRUE  
  840 IF YT>2000 THEN ENFI=TRUE  
  850 GCOL 0,3:PLOT 69,XT,YT
  860 UNTIL ENFI=TRUE
  870 VDU5:GCOL 0,0:MOVE XT-15,YT+15:PRINT CHR$(254):VDU4:GCOL 0,3  
  880 IF DIG=TRUE THEN LD=LD-1
  890 IF WALL=FALSE THEN 930
  900 FOR WD=0 TO 3
  910 VDU5:GCOL 0,3:MOVE XT-15,YT+14+WD*25:PRINT CHR$(255):VDU4    
  920 NEXT
  930 IF LD>0 THEN 710
  940 RETURN
  950 VDU 23,255,255,255,255,255,255,255,255,255
  960 VDU 23,254,60,126,255,255,255,255,126,60     
  970 VDU 23,253,24,24,60,126,126,126,255,255
  980 RETURN
  990 VDU4
 1000 IF KILL1=TRUE THEN PRINT TAB(1,1)"PLAYER 2 WINS":P2S=P2S+1
 1010 IF KILL2=TRUE THEN PRINT TAB(1,1)"PLAYER 1 WINS":P1S=P1S+1
 1020 FOR J=1 TO 100000:NEXT
 1030 D$=INKEY$(0)
 1040 IF D$<>"" THEN 1030
 1050 PRINT "PRESS A KEY"
 1060 D$=GET$
 1070 IF TUR=TRUE THEN 1670
 1080 CLS:GOSUB 1120
 1090 TURN=1:ROU=500:TUR=FALSE
 1100 CLS:GOSUB 1420
 1110 GOTO 50
 1120 CLS
 1130 VDU4
 1140 PRINT TAB(10,5)"TERRA"
 1150 PRINT TAB(5,12)"BY ANDREW(SCORPION)BROOMFIELD"
 1160 FOR K=1 TO 500000:NEXT
 1170 PRINT TAB(5,14)"A:PLAY B:TOURNAMENT C:QUIT"
 1180 WI$=GET$
 1190 IF WI$="C" THEN MODE7:END
 1200 IF WI$="B" THEN GOSUB 1580
 1210 IF WI$="A" THEN RETURN
 1220 GOTO 1120
 1230 PX1=X1:PY1=Y1:PX2=X2:PY2=Y2
 1240 VDU5:GCOL 0,1:MOVE X1,Y1:PRINT CHR$(253):GCOL 0,2:MOVE X2,Y2:PRINT CHR$(253)
 1250 GROUND1=FALSE:GROUND2=FALSE
 1260 FOR K=1000 TO 10 STEP-1   
 1270 PLH=TRUE:PLH2=TRUE
 1280 FOR Q=0 TO 29
 1281 IF K>PY1-30 THEN 1291      
 1290 IF POINT(X1+Q,K)=3 THEN PLH=FALSE:GROUND1=TRUE
 1291 IF K>PY2-30 THEN 1310    
 1300 IF POINT(X2+Q,K)=3 THEN PLH2=FALSE:GROUND2=TRUE
 1310 NEXT Q
 1320 IF GROUND1=TRUE THEN 1340
 1330 IF PLH=TRUE THEN Y1=K+30
 1340 IF GROUND2=TRUE THEN 1360
 1350 IF PLH2=TRUE THEN Y2=K+30
 1360 NEXT K
 1370 GCOL 0,0:MOVE PX1,PY1:PRINT CHR$(253):GCOL 0,0:MOVE PX2,PY2:PRINT CHR$(253)
 1380 GCOL 0,0:MOVE X1,Y1:PRINT CHR$(255):GCOL 0,0:MOVE X2,Y2:PRINT CHR$(255)
 1390 GCOL 0,1:MOVE X1,Y1:PRINT CHR$(253):GCOL 0,2:MOVE X2,Y2:PRINT CHR$(253)
 1400 VDU4
 1410 RETURN
 1420 CLS
 1430 PRINT "COLOUR SCHEMES AVALIBLE"
 1440 PRINT "A:NORMAL":PRINT "B:SUNRISE":PRINT "C:DAYTIME":PRINT "D:TWIGHLIGHT":PRINT "E:NIGHTTIME"
 1450 COL=1
 1460 OPT$=GET$
 1470 IF OPT$="A" THEN COL=1
 1480 IF OPT$="B" THEN COL=2
 1490 IF OPT$="C" THEN COL=3
 1500 IF OPT$="D" THEN COL=4
 1510 IF OPT$="E" THEN COL=5
 1520 IF COL=1 THEN VDU 19,0,0,0,0,0:VDU 19,1,1,0,0,0  
 1530 IF COL=2 THEN VDU 19,0,11,0,0,0:VDU 19,1,1,0,0,0
 1540 IF COL=3 THEN VDU 19,0,6,0,0,0:VDU 19,3,3,0,0,0 
 1550 IF COL=4 THEN VDU 19,0,4,0,0,0:VDU 19,2,5,0,0,0 
 1560 IF COL=5 THEN VDU 19,0,0,0,0,0:VDU 19,2,5,0,0,0
 1570 RETURN
 1580 CLS
 1590 PRINT "TOURNAMENT"  
 1600 PRINT:PRINT "PLAY 10 ROUNDS":PRINT "IF DRAW AFTER 10 ROUNDS THEN":PRINT "PLAY 2 MORE ROUNDS IN":PRINT "SUDDEN DEATH":PRINT
 1610 P1S=0:P2S=0:ROU=0:TURN=1:TUR=TRUE  
 1620 INPUT "PLAYER 1 ";NAME1$:INPUT "PLAYER 2 ";NAME2$
 1630 REPEAT
 1640 GOSUB 1420
 1650 ROU=ROU+1:TUR=TRUE
 1660 GOTO 50
 1670 IF TURN=1 THEN TURN=2 ELSE TURN=1
 1680 CLS:PRINT "TOURNAMENT SCORES AFTER ";ROU;" ROUNDS"
 1690 PRINT:PRINT NAME1$;" ";P1S;"  ";NAME2$;" ";P2S
 1700 FOR K=1 TO 300000:NEXT     
 1710 PRINT:PRINT:PRINT "PRESS A KEY"
 1720 D$=GET$
 1730 IF ROU=PR THEN TUR=FALSE   
 1740 UNTIL TUR=FALSE
 1750 CLS
 1760 IF P1S>P2S THEN PRINT NAME1$;" WINS":GOTO 1820
 1770 IF P2S>P1S THEN PRINT NAME2$;" WINS":GOTO 1820
 1780 IF P1S=P2S THEN PRINT "SUDDEN DEATH -- PLAY 2 MORE ROUNDS"
 1790 PRINT "PRESS A KEY":WI$=GET$
 1800 PR=PR+2:GOTO 1630
 1810 PR=10
 1820 PRINT:PRINT "PRESS A KEY"
 1830 WAIT$=GET$ 
 1840 RETURN
