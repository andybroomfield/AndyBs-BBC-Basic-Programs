   10 MODE 1
   20 VDU 23,255,0,126,126,126,126,126,126,0
   30 DIM CAN(26,22) 
   40 CLS:PRINT "WORMS MOASIC" 
   50 PRINT "USE YOUR WORM TO CONTROL"
   60 PRINT "AS MANY SQUARES AS POSSIBLE"
   70 PRINT "A=UP Z=DOWN O=LEFT P=RIGHT"
   71 PRINT
   72 INPUT "HOW MUCH TIME (IN SECONDS) ";TIM
   75 PRINT:PRINT "PRESS A KEY"
   80 WAIT$=GET$
   81 CLS
   90 FOR K=1 TO 10000:NEXT K
  100 FOR K=1 TO 26
  110 FOR I=1 TO 22
  120 CAN(K,I)=0
  121 COLOUR 3:PRINT TAB(K,I);CHR$(255)
  130 NEXT I
  140 NEXT K
  150 PLYS=0:CPUS=0
  160 XP=RND(26):YP=RND(22):XC=RND(26):YC=RND(22)
  170 TIME=0
  180 XTAG=RND(26):YTAG=RND(22) 
  200 DIR=0
  210 REPEAT
  211 COLOUR 3:PRINT TAB(30,1)"TIME"     
  212 PRINT TAB(30,2);(TIM*100-TIME) DIV 100;" "
  220 PXP=XP:PYP=YP:PXC=XC:PYC=YC
  230 A$=INKEY$(0)
  240 IF A$="A" OR A$="a" THEN DIR=1   
  250 IF A$="Z" OR A$="z" THEN DIR=2 
  260 IF A$="O" OR A$="o" THEN DIR=3 
  270 IF A$="P" OR A$="p" THEN DIR=4   
  280 IF DIR=1 THEN YP=YP-1 
  290 IF DIR=2 THEN YP=YP+1
  300 IF DIR=3 THEN XP=XP-1 
  310 IF DIR=4 THEN XP=XP+1 
  320 IF YP<1 THEN YP=1
  330 IF YP>22 THEN YP=22
  340 IF XP<1 THEN XP=1
  350 IF XP>26 THEN XP=26
  360 IF YC>YTAG THEN YC=YC-1:GOTO 400  
  370 IF YC<YTAG THEN YC=YC+1:GOTO 400  
  380 IF XC>XTAG THEN XC=XC-1           
  390 IF XC<XTAG THEN XC=XC+1    
  400 IF YC=YTAG AND XC=XTAG THEN XTAG=RND(26):YTAG=RND(22) 
  410 CAN(XP,YP)=CAN(XP,YP)+1
  420 IF CAN(XP,YP)>1 THEN CAN(XP,YP)=1
  430 CAN(XC,YC)=CAN(XC,YC)-1
  440 IF CAN(XC,YC)<-1 THEN CAN(XC,YC)=-1
  450 COLOUR 1:PRINT TAB(XP,YP)"*"
  460 IF CAN(PXP,PYP)>0 THEN COLOUR 1:PRINT TAB(PXP,PYP);CHR$(255)
  470 IF CAN(PXP,PYP)<1 THEN COLOUR 3:PRINT TAB(PXP,PYP);CHR$(255)
  480 COLOUR 2:PRINT TAB(XC,YC)"*"
  490 IF CAN(PXC,PYC)<0 THEN COLOUR 2:PRINT TAB(PXC,PYC);CHR$(255)  
  500 IF CAN(PXC,PYC)>-1 THEN COLOUR 3:PRINT TAB(PXC,PYC);CHR$(255)  
  510 FOR K=1 TO 10000:NEXT K
  520 UNTIL TIME>TIM*100
  522 COLOUR 3
  530 PRINT TAB(30,5)"ASSESING"
  540 PRINT TAB(30,6)"SCORE"
  550 FOR K=1 TO 26
  560 FOR I=1 TO 22
  570 IF CAN(K,I)=1 THEN PLYS=PLYS+1:SOUND 1,-15,PLYS/2,1
  580 IF CAN(K,I)=-1 THEN CPUS=CPUS+1:SOUND 3,-15,CPUS/2,1
  590 PRINT TAB(29,8)"PLAYER"
  600 PRINT TAB(29,9);PLYS
  610 PRINT TAB(29,11)"COMPUTER"
  620 PRINT TAB(29,12);CPUS
  630 NEXT I
  640 NEXT K
  650 IF PLYS>CPUS THEN PRINT TAB(29,14)"YOU WIN"
  660 IF CPUS>PLYS THEN PRINT TAB(29,14)"YOU LOSE"
  670 PRINT TAB(29,16)"PLAY AGAIN"
  677 REPEAT:A$=INKEY$(0):UNTIL A$=""
  680 WAIT$=GET$
  690 IF WAIT$="Y" OR WAIT$="y" THEN GOTO 40
  700 MODE7
  710 END
